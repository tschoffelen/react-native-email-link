import groovy.json.JsonSlurper

def readPackageJson() {
    def slurper = new JsonSlurper()
    return slurper.parse(file('../package.json'), "utf-8")
}

def computeVersionName() {
    return readPackageJson().version
}

def computeVersionCode() {
    return readPackageJson().versionCode
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    ext {
        minSdkVersion = 16
        compileSdkVersion = 29
        targetSdkVersion = 29
        gradlePluginVersion = "3.5.1"
        reactNativeVersion = "+" // The react native versioning on jcenter is messed up: https://github.com/facebook/react-native/issues/13094
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${gradlePluginVersion}"
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode computeVersionCode()
        versionName computeVersionName()
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
}

repositories {
    mavenCentral()
    maven {
        url "$projectDir/../../react-native/android"
    }
    jcenter()
    google()
}

dependencies {
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
}
